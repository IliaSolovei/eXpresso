apply plugin: 'kotlin-multiplatform'
apply plugin: 'com.android.library'
apply plugin: 'kotlinx-serialization'

android {
    compileSdkVersion 29
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildToolsVersion = '29.0.2'

    sourceSets {
        getByName("main") {
            manifest.srcFile("src/androidMain/AndroidManifest.xml")
            java.srcDirs("src/androidMain/kotlin")
            res.srcDirs("src/androidMain/res")
        }
    }
}

kotlin {
    targets {
        ios('iOS') {
            binaries {
                framework {
                    baseName = "sharedTests"
                    embedBitcode("disable")

                    transitiveExport = true
                    export project(':kuiks-core')

                    linkerOpts("-F$projectDir/../Carthage/Build/iOS")
                }
            }
        }

        fromPreset(presets.android, 'android')
    }

    sourceSets {
        commonMain.dependencies {
            implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'
            implementation "org.jetbrains.kotlin:kotlin-test-annotations-common:$kotlin_version"
            implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:0.20.0"
            implementation 'dev.michallaskowski.mokttp:mokttp-common:0.1.0'

            api project(':kuiks-core')
        }

        iOSMain.dependencies {
            implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:0.20.0"
            api project(':kuiks-core')
        }

        androidMain.dependencies {
            implementation 'org.jetbrains.kotlin:kotlin-stdlib'
            implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.20.0"
            implementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
        }
    }
}

task fatDebugFramework(type: org.jetbrains.kotlin.gradle.tasks.FatFrameworkTask) {
    baseName = "sharedTests"
    from(
            kotlin.targets.iOSX64.binaries.getFramework("DEBUG"),
            kotlin.targets.iOSArm64.binaries.getFramework("DEBUG")
    )
}
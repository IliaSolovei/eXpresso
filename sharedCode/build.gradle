apply plugin: 'kotlin-multiplatform'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.android.library'

apply from: rootProject.file("gradle/publish.gradle")

android {
    compileSdkVersion 29
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildToolsVersion = '29.0.2'

    sourceSets {
        getByName("main") {
            manifest.srcFile("src/androidMain/AndroidManifest.xml")
            java.srcDirs("src/androidMain/kotlin")
            res.srcDirs("src/androidMain/res")
        }
    }
}

kotlin {
    targets {
        ios('iOS') {
            binaries {
                framework {
                    baseName = "kuiks"
                    embedBitcode("disable")
                }
            }

            def targetArchitecture = it.name
            compilations.main {
                cinterops {
                    xctest {
                        // xctest_*.def require Xcode or symlink to be at /Applications/Xcode.app
                        defFile project.file("src/iosMain/xctest_${targetArchitecture}.def")
                    }
                }
            }
        }

        fromPreset(presets.android, 'android') {
            publishLibraryVariants("release", "debug")
        }
    }

    sourceSets {
        commonMain.dependencies {
            implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'
            implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core-common:1.3.5-native-mt'
        }

        androidMain.dependencies {
            implementation 'org.jetbrains.kotlin:kotlin-stdlib'
            implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.5-native-mt'

            implementation 'androidx.test.ext:junit:1.1.1'
            implementation 'androidx.test.espresso:espresso-core:3.2.0'
            implementation 'androidx.test.espresso:espresso-contrib:3.2.0'
        }

        iOSMain.dependencies {
            implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core-native:1.3.5-native-mt'
        }
    }
}